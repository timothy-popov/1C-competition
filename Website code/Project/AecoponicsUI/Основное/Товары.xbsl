@ВПодсистеме
структура Товар
    знч Наименование: Строка
    знч Цена: Число
    знч КодСинхронизации: Строка
;

@ВПодсистеме
метод ЗаписатьОбновитьТовар(Товар: Товар): Булево
    знч Ссылка = ПолучитьТовар(Товар.КодСинхронизации)
    
    пер Объект: Товары.Объект
    
    если Ссылка == Неопределено
        Объект = новый Товары.Объект()
    иначе
        Объект = Ссылка.ЗагрузитьОбъект()
    ;
    
    Объект.Наименование = Товар.Наименование
    Объект.Цена = Товар.Цена
    Объект.КодСинхронизации = Товар.КодСинхронизации
    Объект.Записать()

    возврат Истина
;

@ВПроекте
метод ПолучитьТовар(Код: Строка): Товары.Ссылка?
    
    возврат Запрос{
        ВЫБРАТЬ
            Товары.Ссылка
        ИЗ
            Товары КАК Товары
        ГДЕ
            Товары.КодСинхронизации == %Код
    }.Выполнить().ЕдинственныйИлиУмолчание()?.Ссылка
;

@ВПроекте
метод ПолучитьЦену(Товар: Товары.Ссылка): Число
    возврат Запрос{
        ВЫБРАТЬ
            Товары.Цена
        ИЗ
            Товары КАК Товары
        ГДЕ
            Товары.Ссылка == %Товар
    }.Выполнить().ЕдинственныйИлиУмолчание()?.Цена
;